plugins {
	id 'fabric-loom' version '1.0-SNAPSHOT'
	id 'maven-publish'
	id 'eclipse'
	id 'idea'
	id 'java-library'
}

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

loom {
	runtimeOnlyLog4j = true

	mixin {
		defaultRefmapName = "mixins.lunaslimes.refmap.json"
	}

	accessWidenerPath = file("src/main/resources/lunaslimes.accesswidener")
	interfaceInjection {
		// When enabled, injected interfaces from dependencies will be applied.
		enableDependencyInterfaceInjection = false
	}
}

configurations {
	includeModImplementation
	includeImplementation

	include.extendsFrom includeImplementation
	include.extendsFrom includeModImplementation

	implementation.extendsFrom includeImplementation
	modImplementation.extendsFrom includeModImplementation
}

repositories {
	maven {
		url = 'https://jitpack.io'
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"

		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		url = 'https://maven.terraformersmc.com'

		content {
			includeGroup "com.terraformersmc"
		}
	}
	maven {
		url = 'https://maven.shedaniel.me/'
	}
	maven {
		url "https://maven.flashyreese.me/releases"
	}
	maven {
		url "https://maven.flashyreese.me/snapshots"
	}
	maven {
		url = "https://minecraft.guntram.de/maven/"
	}
	flatDir {
		dirs 'libs'
	}
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings(loom.layered {
		it.officialMojangMappings {
			setNameSyntheticMembers(false)
		}
	})

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modApi("io.github.llamalad7:mixinextras-fabric:${mixin_extras_version}")

	// FrozenLib
	modImplementation("maven.modrinth:frozenlib:${frozenlib_version}")

	// Mod Menu
	modImplementation("com.terraformersmc:modmenu:${modmenu_version}")

	// Cloth Config
	modImplementation("me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}") {
		exclude(group: "net.fabricmc.fabric-api")
		exclude(group: "com.terraformersmc")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

javadoc {
	options.tags = ["reason"]
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
	options.fork = true
	options.incremental = true
}

tasks.withType(Test).configureEach {
	maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	repositories {

	}
}
